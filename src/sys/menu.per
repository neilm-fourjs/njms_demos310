
&include "schema.inc"

LAYOUT ( TEXT="Menu Screen", STYLE="centered grad2", MINWIDTH=50, MINHEIGHT=16  )
HBOX
GRID hpadding2
{
[hsp1]
}
END -- GRID
GROUP
VBOX
GRID (STYLE="menu grad1")
{
 [titl                             ]
<T menu                             >
 [desc                             ]
 [desc                             ]
 [desc                             ]
 [desc                             ]
 [desc                             ]
 [desc                             ]
 [desc                             ]
 [desc                             ]
 [desc                             ]
 [desc                             ]
 [desc                             ]
 [desc                             ]
<                                   >
}
END -- GRID
GRID (STYLE="menu grad1")
{
 [: :lb:f001                  :d  :]
 [:  :back :close:logout:about:   :]
 [sp1:logo                     :sp2]
     [                         ]
}
END -- GRID
END -- VBOX
END -- GROUP
GRID hpadding3
{
[hsp2]
}
END -- GRID
END -- HBOX
TABLES
sys_menus

ATTRIBUTES
IMAGE hsp1 : spacer, IMAGE="fa-fw", STRETCH=X, STYLE="noborder";
IMAGE hsp2 : spacer, IMAGE="fa-fw", STRETCH=X, STYLE="noborder";
--IMAGE vsp1 : spacer, IMAGE="fa-fw", STRETCH=Y, STYLE="noborder";
--IMAGE vsp2 : spacer, IMAGE="fa-fw", STRETCH=Y, STYLE="noborder";

LABEL titl = FORMONLY.l_titl,STYLE="heading",JUSTIFY=CENTER;

TABLE menu : menu, STYLE="menu oneclick",WANTFIXEDPAGESIZE ;
PHANTOM sys_menus.menu_key;
PHANTOM sys_menus.m_id;
PHANTOM sys_menus.m_pid;
PHANTOM sys_menus.m_type;
desc = sys_menus.m_text,STYLE="menuopts em14",IMAGECOLUMN=m_img;
PHANTOM sys_menus.m_item;
PHANTOM sys_menus.m_passw;
PHANTOM FORMONLY.m_img;

LABEL lb : lb, TEXT=%"User:",STYLE="small";
LABEL f001 = FORMONLY.m_user, SIZEPOLICY=DYNAMIC,STYLE="small";

d = FORMONLY.l_dummy, STYLE="hidden",INVISIBLE,SAMPLE=".";

--BUTTON accept : accept, TEXT=%"Select", IMAGE="fa-arrow-circle-right";
BUTTON back : back, TEXT=%"Back", IMAGE="fa-arrow-circle-left";
BUTTON close : close, TEXT="Quit", IMAGE="fa-power-off";
BUTTON logout : logout, TEXT="Quit + Logout", IMAGE="fa-power-off";
BUTTON about : about, TEXT="About", IMAGE="help";

IMAGE sp1 : spacer, IMAGE="fa-fw", STRETCH=X, STYLE="noborder";
IMAGE logo = FORMONLY.logo, AUTOSCALE, SIZEPOLICY=INITIAL,STRETCH=NONE, STYLE="noborder center";
IMAGE sp2 : spacer, IMAGE="fa-fw", STRETCH=X, STYLE="noborder";

INSTRUCTIONS
SCREEN RECORD menu [12] ( menu_key THRU m_img )
